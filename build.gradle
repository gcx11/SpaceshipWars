buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.61'
}

apply plugin: 'kotlin-dce-js'

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    mavenCentral()
}

def coroutines_version = '1.3.3'
def ktor_version = '1.2.6'
def logback_version = '1.2.3'
def kotlinx_io_version = '0.1.16'
def atomicfu_version = '0.14.1'

kotlin {
    jvm()
    js {
        browser {
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io:$kotlinx_io_version"
                implementation "org.jetbrains.kotlinx:atomicfu-common:$atomicfu_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.ktor:ktor-server-netty:$ktor_version"
                implementation "io.ktor:ktor-html-builder:$ktor_version"
                implementation "io.ktor:ktor-websockets:$ktor_version"
                implementation "ch.qos.logback:logback-classic:$logback_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlinx_io_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "io.ktor:ktor-client-js:$ktor_version"
                implementation "io.ktor:ktor-client-websockets:$ktor_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
                implementation(npm("text-encoding", "0.7.0")) // workaround for https://github.com/Kotlin/kotlinx-io/issues/57
                implementation "org.jetbrains.kotlinx:kotlinx-io-js:$kotlinx_io_version"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

runDceJsKotlin {
    dceOptions {
        outputDirectory = "${rootProject.buildDir}/js/packages/${rootProject.name}/kotlin-dce"
    }
}

jsBrowserWebpack.dependsOn runDceJsKotlin
jsBrowserRun.dependsOn runDceJsKotlin

jvmJar {
    dependsOn(jsBrowserWebpack)
    from(new File(jsBrowserWebpack.entry.name, jsBrowserWebpack.outputPath))
}

task run(type: JavaExec, dependsOn: [jvmJar]) {
    group = "application"
    main = "me.gcx11.spaceshipwars.ServerKt"
    classpath(configurations.jvmRuntimeClasspath, jvmJar)
    args = []
}